plugins {
    id "groovy"
    id "idea"
    id "com.energizedwork.webdriver-binaries" version "1.4"
}

ext {
    // The drivers we want to use
    drivers = ["firefox"]

    ext {
        groovyVersion = '2.5.4'
        gebVersion = '2.3.1'
        seleniumVersion = '3.141.0'
        chromeDriverVersion = '2.32'
        geckoDriverVersion = '0.24.0'
    }
}

repositories {
    mavenCentral()
}

dependencies {
    testCompile "org.codehaus.groovy:groovy-all:$groovyVersion"
    testCompile "org.spockframework:spock-core:1.2-groovy-2.5"
    testCompile "org.gebish:geb-spock:$gebVersion"
    testCompile "org.seleniumhq.selenium:selenium-support:$seleniumVersion"
    testCompile "org.seleniumhq.selenium:selenium-chrome-driver:$seleniumVersion"
    testCompile "org.seleniumhq.selenium:selenium-firefox-driver:$seleniumVersion"
    testCompile "org.codehaus.groovy.modules.http-builder:http-builder:0.7"
}

webdriverBinaries {
    chromedriver chromeDriverVersion
    geckodriver geckoDriverVersion
}

drivers.each { driver ->
    task "${driver}Test"(type: Test) {
        group JavaBasePlugin.VERIFICATION_GROUP

        outputs.upToDateWhen { false }  // Always run tests

        systemProperty "geb.build.reportsDir", reporting.file("geb/$name")
        systemProperty "geb.env", driver
    }
}

test {
    dependsOn drivers.collect { tasks["${it}Test"] }
    enabled = false
}

tasks.withType(Test) {
    maxHeapSize = "1g"
    jvmArgs "-XX:MaxMetaspaceSize=128m"
    testLogging {
        exceptionFormat = "full"
    }
}

tasks.withType(GroovyCompile) {
    groovyOptions.forkOptions.memoryMaximumSize = "256m"
}
